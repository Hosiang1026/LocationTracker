plugins {
    id 'com.android.application'
}

import java.util.Properties
import java.io.FileInputStream

def localProps = new Properties()
def localPropsFile = rootProject.file('local.properties')
if (localPropsFile.exists()) {
    localProps.load(new FileInputStream(localPropsFile))
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.hx.locationtracker"
        minSdkVersion 14
        targetSdkVersion 33
        versionCode 216
        versionName "2.1.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "CONFIG_URL", '"' + (localProps["CONFIG_URL"] ?: "https://nas.haoxiang.eu.org/locationtracker/api/config") + '"'
        buildConfigField "String", "HEARTBEAT_URL", '"' + (localProps["HEARTBEAT_URL"] ?: "https://nas.haoxiang.eu.org/locationtracker/api/config/heartbeat") + '"'
        buildConfigField "String", "WEBHOOK_URL", '"' + (localProps["WEBHOOK_URL"] ?: "https://ha.haoxiang.eu.org/api/webhook/db72ebc1627e52685ca64cdb3807dfbc61cf1876c0ce0f5eafecb975b577609b") + '"'
    }

    signingConfigs {
        release {
            def props = new Properties()
            def propFile = rootProject.file('local.properties')
            if (propFile.exists()) {
                props.load(new FileInputStream(propFile))
            }
            storeFile file("keystore/release_v2.keystore")
            storePassword props['KEYSTORE_PASSWORD'] ?: ""
            keyAlias props['KEY_ALIAS'] ?: ""
            keyPassword props['KEY_PASSWORD'] ?: ""
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro','../libModule/proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            shrinkResources false
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            zipAlignEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def version = variant.versionName
            def buildType = variant.buildType.name
            def newApkName = "LocationTracker-${buildType}-v${version}.apk"
            outputFileName = newApkName
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.work:work-runtime:2.7.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}